
:- table nw_activation_from_yw_step/5.
nw_activation_from_yw_step(ActivationId, FunctionName, StepId, StepName, ActivationLine) :-
    nw_function_activation(ActivationId, _, FunctionName, ActivationLine, _),
    yw_workflow_step(StepId, StepName, _, _,  StepBeginLine, StepEndLine),
    line_inside_range(ActivationLine, StepBeginLine, StepEndLine).

:- table nw_activation_into_yw_program/6.
nw_activation_into_yw_program(ActivationId, FunctionName, StepId, StepName, ActivationLine, CallerActivationId) :-
    yw_program(StepId, StepName, _, _,  StepBeginLine, StepEndLine),
    nw_function_activation(ActivationId, _, FunctionName, ActivationLine, CallerActivationId),
    line_outside_range(ActivationLine, StepBeginLine, StepEndLine),
    nw_function_definition(_, FunctionName, FunctionStartLine, _, _),
    line_inside_range(FunctionStartLine, StepBeginLine, StepEndLine).

:- table _nw_activation_into_yw_step_subprogram/2.
_nw_activation_into_yw_step_subprogram(ActivationId, StepId) :-
    nw_activation_into_yw_program(ActivationId, _, StepId, _, _, _),
    nw_activation_into_yw_program(ActivationId, _, SubprogramId, _, _, _),
    yw_workflow_step(SubprogramId, _, StepId, _, _, _).

:- table nw_activation_into_yw_step/6.
nw_activation_into_yw_step(ActivationId, FunctionName, StepId, StepName, ActivationLine, CallerActivationId) :-
    nw_activation_into_yw_program(ActivationId, FunctionName, StepId, StepName, ActivationLine, CallerActivationId),
    not _nw_activation_into_yw_step_subprogram(ActivationId, StepId).

:- table nw_variable_for_yw_in_port/9.
nw_variable_for_yw_in_port(VariableId, VariableName, VariableValue, DataId, DataName, PortId, PortName, StepId, StepName) :-
    yw_step_input(StepId, _, _, PortId, PortName, DataId, DataName),
    yw_workflow_step(StepId, StepName,_,_, StepBeginLine, StepEndLine),
    PortName = VariableName,
    nw_variable_usage(_, _, VariableId, VariableName, _, UsageLine),
    line_inside_range(UsageLine, StepBeginLine, StepEndLine),
    nw_variable_assignment(_, VariableId, VariableName, AssignmentLine, VariableValue),
    line_outside_range(AssignmentLine, StepBeginLine, StepEndLine).

:- table nw_variable_for_yw_out_port/9.
nw_variable_for_yw_out_port(VariableId, VariableName, VariableValue, DataId, DataName, PortId, PortName, StepId, StepName) :-
    yw_step_output(StepId, _, _, PortId, PortName, DataId, DataName),
    yw_workflow_step(StepId, StepName,_,_, StepBeginLine, StepEndLine),
    PortName = VariableName,
    nw_variable_assignment(_, VariableId, VariableName, AssignmentLine, VariableValue),
    line_inside_range(AssignmentLine, StepBeginLine, StepEndLine),
    nw_variable_usage(_, _, VariableId, VariableName, _, UsageLine),
    line_outside_range(UsageLine, StepBeginLine, StepEndLine).

:- table nw_variable_for_yw_data/5.
nw_variable_for_yw_data(VariableId, VariableName, VariableValue, DataId, DataName) :-
    nw_variable_for_yw_in_port(VariableId, VariableName, VariableValue, DataId, DataName, _, _, _, _).
nw_variable_for_yw_data(VariableId, VariableName, VariableValue, DataId, DataName) :-
    nw_variable_for_yw_out_port(VariableId, VariableName, VariableValue, DataId, DataName, _, _, _, _).
