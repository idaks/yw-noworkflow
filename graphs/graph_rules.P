
graph_begin(Name, Title, Layout) :-
    nl,
    write('digraph '), write(Name), writeln(' {'),
    write('rankdir='), writeln(Layout),
    writeln('fontname=Courier; fontsize=18; labelloc=t'),
    write('label="'), write(Title), writeln('"').

start_visible_cluster_box(Name) :-
    write('subgraph cluster_'), write(Name), write(' { label=""; color=black; penwidth=2'), nl,
    write('subgraph cluster_'), write(Name), write('_inner { label=""; color=white'), nl.

start_invisible_cluster_box(Name) :-
    write('subgraph cluster_'), write(Name), write(' { label=""; color=white'), nl,
    write('subgraph cluster_'), write(Name), write('_inner { label=""; color=white'), nl.

end_cluster_box() :-
    writeln('}}').

data_node_style() :-
    writeln('node[shape=box style="rounded,filled" fillcolor="#FFFFCC" peripheries=1 fontname=Helvetica]').

workflow_port_style() :-
    writeln('node[shape=circle fillcolor="#FFFFFF" peripheries=1 width=0.2]').

graph_end() :-
    writeln('}'),
    nl.

graph_data_nodes(WorkflowId) :-
    yw_data(_, DataName, WorkflowId, _),
    writeq(DataName), nl,
    fail
    ;
    true.

graph_data_to_data_edges(WorkflowId) :-
    yw_data(DownstreamDataId, DownstreamDataName, WorkflowId, _),
    yw_data(UpstreamDataId, UpstreamDataName, WorkflowId, _),
    yw_data_immediately_downstream(UpstreamDataId, DownstreamDataId),
    writeq(UpstreamDataName),
    write(' -> '),
    writeq(DownstreamDataName), nl,
    fail
    ;
    true.

graph_inflow_nodes(WorkflowId) :-
    yw_inflow_data(WorkflowId, _, _, DataName),
    write(DataName), write('_inflow [label=""]'), nl,
    fail
    ;
    true.

graph_outflow_nodes(WorkflowId) :-
    yw_outflow_data(WorkflowId, _, _, DataName),
    write(DataName), write('_outflow [label=""]'), nl,
    fail
    ;
    true.

graph_inflow_to_data_edges(WorkflowId) :-
    yw_inflow_data(WorkflowId, _, _, DataName),
    write(DataName), write('_inflow'),
    write(' -> '),
    writeq(DataName), nl,
    fail
    ;
    true.

graph_data_to_outflow_edges(WorkflowId) :-
    yw_outflow_data(WorkflowId, _, _, DataName),
    writeq(DataName),
    write(' -> '),
    write(DataName), write('_outflow'), nl,
    fail
    ;
    true.

:- table yw_inflow_data/4.
yw_inflow_data(WorkflowId, WorkflowName, DataId, DataName) :-
    yw_inflow(WorkflowId, WorkflowName, DataId, DataName, _, _).

:- table yw_outflow_data/4.
yw_outflow_data(WorkflowId, WorkflowName, DataId, DataName) :-
    yw_outflow(_, _, DataId, DataName, WorkflowId, WorkflowName).


:- table yw_data_immediately_downstream/2.
yw_data_immediately_downstream(UpstreamDataId,DownstreamDataId) :-
    yw_step_input(StepId, _, _, _, _, UpstreamDataId, _),
    yw_step_output(StepId, _, _, _, _, DownstreamDataId, _).
