
nw_activation_from_yw_step(StepName, FunctionName, ActivationLine, ActivationId) :-
    nw_function_activation(ActivationId, FunctionName, ActivationLine, _),
    yw_workflow_step(_, StepName, _, _,  StepBeginLine, StepEndLine),
    nw_line_inside_yw_line_range(ActivationLine, StepBeginLine, StepEndLine).

activation_argument_variable(ActivationId, ActivatedFuncName,
                             FormalParamName, ActualParamName, Value) :-
    call_from_top_function(ActivationId, ActivatedFuncName, _),
    object_value(_, ActivationId, _, FormalParamName, Value, _),
    dependency(_, _, ActivationId, _, CallerActivationId, ActualParamId),
    usage(_, CallerActivationId, ActualParamId, _, ActualParamName, _).

activation_argument_literal(ActivationId, FuncName, FormalParamName, Value) :-
    call_from_top_function(ActivationId, FuncName, _),
    object_value(_, ActivationId, _, FormalParamName, Value, _),
    not activation_argument_variable(ActivationId, FuncName, FormalParamName, _, Value).

nw_line_inside_yw_line_range(Line, RangeStart, RangeEnd) :-
    Line >= RangeStart,
    Line =< RangeEnd.

nw_line_outside_yw_line_range(Line, RangeStart, _) :-
    Line < RangeStart.
nw_line_outside_yw_line_range(Line, _, RangeEnd) :-
    Line > RangeEnd.

:- table nw_variable_for_yw_in_port/5.
nw_variable_for_yw_in_port(VariableId, VariableName, VariableValue, PortId, PortName) :-
    yw_in_port(StepId, _, PortId, PortName, _, _),
    yw_workflow_step(StepId, _,_,_, StepBeginLine, StepEndLine),
    PortName = VariableName,
    nw_variable_usage(_, _, VariableId, VariableName, _, UsageLine),
    nw_line_inside_yw_line_range(UsageLine, StepBeginLine, StepEndLine),
    nw_variable_assignment(_, VariableId, VariableName, AssignmentLine, VariableValue),
    nw_line_outside_yw_line_range(AssignmentLine, StepBeginLine, StepEndLine).
