

nw_script_activation(Script, Command, ScriptActivation, Docstring) :-
    trial(_,_,_, Script, _, Command, _,_,_, Docstring),
    activation(_, ScriptActivation, _,_,_,_, nil).

nw_function_definition(FunctionId, Name, FirstLine, LastLine, Docstring) :-
    function_def(_, FunctionId, Name, _, FirstLine, LastLine, Docstring).

nw_function_activation(ActivationId, Name, Line, CallerActivationId) :-
    activation(_, ActivationId, Name, Line, _, _, CallerActivationId),
    CallerActivationId \== nil.

nw_function_call(FunctionId, FunctionCallId, Name) :-
    object(_, FunctionId, FunctionCallId, Name, 'FUNCTION_CALL').

nw_function_argument(ActivationId, FunctionName, ArgumentName, ArgumentId, Value, VariableName, VariableId) :-
    nw_function_argument_variable(ActivationId, FunctionName, ArgumentName, ArgumentId, Value, VariableId, VariableName),
    VariableId \== nil.
nw_function_argument(ActivationId, FunctionName, ArgumentName, ArgumentId, Value, VariableName, VariableId) :-
    nw_function_argument_literal(ActivationId, FunctionName, ArgumentName, ArgumentId, Value),
    VariableId = nil,
    VariableName = nil.

nw_function_argument_variable(ActivationId, FunctionName, ArgumentName, ArgumentId, Value, VariableId, VariableName) :-
    activation(_, ActivationId, FunctionName, _,_,_,_),
    object_value(_, ActivationId, _, ArgumentName, _, 'ARGUMENT'),
    variable(_, ActivationId, ArgumentId, ArgumentName, _, Value, _),
    dependency(_,_, ActivationId, ArgumentId, CallerActivationId, VariableId),
    variable(_, CallerActivationId, VariableId, VariableName, _, Value,_).

nw_function_argument_literal(ActivationId, FunctionName, ArgumentName, ArgumentId, Value) :-
    activation(_, ActivationId, FunctionName, _,_,_,_),
    object_value(_, ActivationId, _, ArgumentName, Value, 'ARGUMENT'),
    not nw_function_argument_variable(ActivationId, FunctionName, ArgumentName, ArgumentId, Value, _,_).

nw_variable_assignment(ActivationId, VariableId, Name, Line, Value) :-
    variable(_, ActivationId, VariableId, Name, Line, Value, _),
    not nw_variable_is_function(VariableId),
    Line \== 0,
    Name \== 'return',
    Value \= 'now(n/a)'.

nw_variable_is_function(VariableId) :-
    variable(_, VariableId, _, Name, Line, _, _),
    function_def(_,_, Name, _, Line, _,_).

nw_variable_usage(UsageId, ActivationId, VariableId, VariableName, VariableValue, Line) :-
    usage(_, ActivationId, VariableId, UsageId, VariableName, Line),
    variable(_,_, VariableId, VariableName, _, VariableValue, _),
    not nw_usage_is_function_call(UsageId, VariableId).

nw_usage_is_function_call(UsageId, VariableId) :-
    usage(_, _, VariableId, UsageId, Name, Line),
    activation(_,_, Name, Line, _,_,_).

nw_variable_dependency(DependencyId, DownstreamActId, DownstreamActName, DownstreamVarId, DownstreamVarName, UpstreamActId, UpstreamActName, UpstreamVarId, UpstreamVarName) :-
    dependency(_, DependencyId, DownstreamActId, DownstreamVarId, UpstreamActId, UpstreamVarId),
    nw_function_activation(DownstreamActId, DownstreamActName,_,_),
    nw_variable_assignment(DownstreamActId, DownstreamVarId, DownstreamVarName,_,_),
    nw_function_activation(UpstreamActId, UpstreamActName,_,_),
    nw_variable_assignment(UpstreamActId, UpstreamVarId, UpstreamVarName,_,_).
